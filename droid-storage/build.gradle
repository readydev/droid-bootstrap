buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
    classpath "com.github.dcendents:android-maven-gradle-plugin:1.4.1"
  }
}

plugins {
  id "com.jfrog.bintray" version "1.7.1"
}

group = "co.mkry"
version = "1.3.0"

apply plugin: "com.android.library"
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "com.github.dcendents.android-maven"

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.0"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 25
  }
}

ext {
  daggerVersion = "2.7"
  jacksonVersion = "2.8.4"
}

dependencies {
  apt "com.google.dagger:dagger-compiler:${daggerVersion}"
  compile project(":droid-bootstrap")
  compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
  compile "com.couchbase.lite:couchbase-lite-android:1.3.1"
}

def siteUrl = "https://github.com/themakery/droid-bootstrap"
def gitUrl = "https://github.com/themakery/droid-bootstrap.git"

if (hasProperty("bintrayUser")) {
  bintray {
    user = bintrayUser
    key = bintrayApiKey

    configurations = ["archives"]
    pkg {
      repo = "android"
      name = project.name
      desc = "Droid Storage"
      websiteUrl = siteUrl
      vcsUrl = gitUrl
      licenses = ["Apache-2.0"]
      publicDownloadNumbers = true
    }
  }
}

install {
  repositories.mavenInstaller {
    pom.project {
      packaging "aar"
    }
  }
}
