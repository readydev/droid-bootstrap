buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
    classpath "com.github.dcendents:android-maven-gradle-plugin:1.4"
  }
}

plugins {
  id "com.jfrog.bintray" version "1.5"
}

group = "co.mkry"
version = "1.2.1"

apply plugin: "com.android.library"
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "com.github.dcendents.android-maven"

android {
  compileSdkVersion 24
  buildToolsVersion "24.0.0"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 24
  }
}

ext {
  daggerVersion = "2.5"
  jacksonVersion = "2.7.5"
}

dependencies {
  apt "com.google.dagger:dagger-compiler:${daggerVersion}"
  compile project(":droid-bootstrap")
  compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
  compile "com.couchbase.lite:couchbase-lite-android:1.2.1"
}

def siteUrl = "https://github.com/themakery/droid-bootstrap"
def gitUrl = "https://github.com/themakery/droid-bootstrap.git"

if (hasProperty("bintrayUser")) {
  bintray {
    user = bintrayUser
    key = bintrayApiKey

    configurations = ["archives"]
    pkg {
      repo = "android"
      name = project.name
      desc = "Droid Storage"
      websiteUrl = siteUrl
      vcsUrl = gitUrl
      licenses = ["Apache-2.0"]
      publicDownloadNumbers = true
    }
  }
}

install {
  repositories.mavenInstaller {
    pom.project {
      packaging "aar"
    }
  }
}
