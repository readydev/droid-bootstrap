group = "co.mkry"
version = "1.2.0"

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
    classpath "com.github.dcendents:android-maven-gradle-plugin:1.3"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6"
  }
}

apply plugin: "com.android.library"
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "com.github.dcendents.android-maven"
apply plugin: "com.jfrog.bintray"

android {
  compileSdkVersion 23
  buildToolsVersion "24.0.0 rc1"

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 23
  }
}

ext {
  daggerVersion = "2.4"
  supportLibVersion = "23.4.0"
  playServicesVersion = "9.0.2"
  rxlifecycleVersion = "0.6.1"
  seedsVersion = "16.0.1"
}

dependencies {
  apt "com.google.dagger:dagger-compiler:${daggerVersion}"
  compile "com.google.dagger:dagger:${daggerVersion}"
  compile "com.google.android.gms:play-services-base:${playServicesVersion}"
  compile "com.google.android.gms:play-services-analytics:${playServicesVersion}"
  compile "com.android.support:appcompat-v7:${supportLibVersion}"
  compile "com.android.support:design:${supportLibVersion}"
  compile "com.android.support:recyclerview-v7:${supportLibVersion}"
  compile "com.android.support:cardview-v7:${supportLibVersion}"
  compile "net.tribe7.seeds:seeds-primitives:${seedsVersion}"
  compile "net.tribe7.seeds:seeds-strings:${seedsVersion}"
  compile "com.trello:rxlifecycle:${rxlifecycleVersion}"
  compile "com.trello:rxlifecycle-components:${rxlifecycleVersion}"
  compile "javax.annotation:javax.annotation-api:1.2"
  compile "com.squareup.okhttp3:okhttp:3.3.1"
  compile "com.squareup.picasso:picasso:2.5.2"
  compile "com.jakewharton.timber:timber:4.1.2"
  compile "com.jakewharton:butterknife:8.0.1"
  compile "io.reactivex:rxandroid:1.2.0"
  compile "io.reactivex:rxjava:1.1.5"
  compile "org.greenrobot:eventbus:3.0.0"
}

def siteUrl = "https://github.com/themakery/droid-bootstrap"
def gitUrl = "https://github.com/themakery/droid-bootstrap.git"

if (hasProperty("bintrayUser")) {
  bintray {
    user = bintrayUser
    key = bintrayApiKey

    configurations = ["archives"]
    pkg {
      repo = "android"
      name = project.name
      desc = "Droid Bootstrap"
      websiteUrl = siteUrl
      vcsUrl = gitUrl
      licenses = ["Apache-2.0"]
      publicDownloadNumbers = true
    }
  }
}

install {
  repositories.mavenInstaller {
    pom.project {
      packaging "aar"
    }
  }
}
